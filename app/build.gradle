import org.gradle.internal.os.OperatingSystem
import java.nio.file.Files
import java.nio.file.StandardCopyOption

apply plugin: 'com.android.application'
// Temporarily disabled - uncomment when you have new google-services.json for UW Madison
// apply plugin: 'com.google.gms.google-services'
// apply plugin: 'com.google.firebase.crashlytics'
apply plugin: "kotlin-android"

def keystorePropertiesFile = rootProject.file("signing.properties")
def keystoreProperties = new Properties()
if (keystorePropertiesFile.exists()) {
    keystoreProperties.load(new FileInputStream(keystorePropertiesFile))
}

android {
    compileSdk 35
    buildToolsVersion "36.0.0"
    flavorDimensions "mindpulse"

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    defaultConfig {
        applicationId "edu.wisc.chm.screenomics"
        minSdkVersion 29
        targetSdkVersion 35
        versionCode 17
        versionName "1.15"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        buildConfigField "boolean", "MINDPULSE_ENABLED", "false"
    }
    productFlavors {
        standard {
            dimension "mindpulse"
        }
        mindpulseDev {
            dimension "mindpulse"
            applicationIdSuffix ".mindpulse"
            versionNameSuffix "-mindpulse"
            buildConfigField "boolean", "MINDPULSE_ENABLED", "true"
        }
    }
    signingConfigs {
        release {
            if (keystorePropertiesFile.exists()) {
                storeFile file(keystoreProperties['storeFile'])
                storePassword keystoreProperties['storePassword']
                keyAlias keystoreProperties['keyAlias']
                keyPassword keystoreProperties['keyPassword']
            }
        }
    }
    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            if (keystorePropertiesFile.exists()) {
                signingConfig signingConfigs.release
            }
        }
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }

    packaging {
        jniLibs {
            useLegacyPackaging = false
        }
    }

    namespace 'com.screenomics'
}

dependencies {
    def core_version = "1.12.0"

    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "androidx.core:core:$core_version"
    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'androidx.preference:preference:1.2.1'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    implementation 'com.google.android.material:material:1.10.0'
    implementation 'androidx.viewpager2:viewpager2:1.0.0'
    implementation 'androidx.fragment:fragment:1.6.1'
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'com.squareup.okhttp3:okhttp:4.11.0'
    implementation 'com.journeyapps:zxing-android-embedded:4.3.0'
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'
    def camerax_version = "1.3.4"
    mindpulseDevImplementation "androidx.camera:camera-core:$camerax_version"
    mindpulseDevImplementation "androidx.camera:camera-camera2:$camerax_version"
    mindpulseDevImplementation "androidx.camera:camera-lifecycle:$camerax_version"
    mindpulseDevImplementation "androidx.camera:camera-view:$camerax_version"
    mindpulseDevImplementation "androidx.camera:camera-video:$camerax_version"
    implementation "androidx.lifecycle:lifecycle-service:2.7.0"
    implementation 'com.google.zxing:core:3.3.0'
    def work_version = "2.9.0"
    implementation "androidx.work:work-runtime:$work_version"
    implementation "androidx.work:work-runtime-ktx:$work_version"
    implementation 'net.danlew:android.joda:2.10.12.2'
    implementation 'com.google.android.gms:play-services-location:21.0.1'
    implementation 'androidx.activity:activity:1.8.2'

    // Firebase temporarily disabled - uncomment after getting new google-services.json
    // implementation platform('com.google.firebase:firebase-bom:30.1.0')
    // implementation 'com.google.firebase:firebase-analytics'
    // implementation 'com.google.firebase:firebase-crashlytics'
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation(platform("org.jetbrains.kotlin:kotlin-bom:1.9.0"))
}

def resolveSdkDirectory = {
    if (android.hasProperty("sdkDirectory") && android.sdkDirectory != null) {
        return android.sdkDirectory
    }
    def envDir = System.getenv("ANDROID_HOME") ?: System.getenv("ANDROID_SDK_ROOT")
    if (envDir) {
        return file(envDir)
    }
    throw new GradleException("Unable to locate Android SDK. Set sdk.dir in local.properties or ANDROID_HOME/ANDROID_SDK_ROOT.")
}

def resolveZipalignExecutable = {
    File sdkDir = resolveSdkDirectory()
    File buildToolsDir = new File(sdkDir, "build-tools")
    String buildToolsVersion = android.buildToolsVersion
    if (!buildToolsVersion) {
        def candidates = buildToolsDir.listFiles()?.findAll { it.isDirectory() }?.collect { it.name }?.sort()
        if (!candidates || candidates.isEmpty()) {
            throw new GradleException("No Android build-tools found in ${buildToolsDir.absolutePath}.")
        }
        buildToolsVersion = candidates.last()
    }
    def execName = OperatingSystem.current().isWindows() ? "zipalign.exe" : "zipalign"
    File executable = new File(buildToolsDir, "${buildToolsVersion}/${execName}")
    if (!executable.exists()) {
        throw new GradleException("zipalign executable not found at ${executable.absolutePath}. Install build-tools ${buildToolsVersion}.")
    }
    return executable
}

def zipalignExecutableProvider = providers.provider { resolveZipalignExecutable() }

def zipalignOutputsTask = tasks.register("zipalignArtifacts") {
    group = "build"
    description = "Aligns generated APK/AAB artifacts on 16 KB boundaries for Google Play compliance."
    doLast {
        File zipalignExe = zipalignExecutableProvider.get()
        List<File> artifacts = []
        File apkDir = new File(buildDir, "outputs/apk")
        File bundleDir = new File(buildDir, "outputs/bundle")
        if (apkDir.exists()) {
            artifacts.addAll(project.fileTree(apkDir).matching { include "**/*.apk" }.files)
        }
        if (bundleDir.exists()) {
            artifacts.addAll(project.fileTree(bundleDir).matching { include "**/*.aab" }.files)
        }
        artifacts = artifacts.findAll { it.exists() }
        if (artifacts.isEmpty()) {
            logger.lifecycle("No APK/AAB artifacts found to zipalign; skipping.")
            return
        }
        artifacts.each { File artifact ->
            File alignedFile = new File(artifact.parentFile, "${artifact.name}.aligned")
            alignedFile.parentFile.mkdirs()
            logger.lifecycle("Running zipalign with 16 KB native alignment on ${artifact.name}")
            project.exec {
                executable = zipalignExe.absolutePath
                args "-f", "-P", "16", "4", artifact.absolutePath, alignedFile.absolutePath
            }
            Files.move(alignedFile.toPath(), artifact.toPath(), StandardCopyOption.REPLACE_EXISTING)
        }
    }
}

tasks.configureEach { task ->
    if (task.name.startsWith("assemble") || task.name.startsWith("bundle")) {
        task.finalizedBy(zipalignOutputsTask)
    }
}
